pkgname=vimb-git
_pkgname=vimb
pkgver=3.6.0.r10.g44cbc4c
pkgrel=1
pkgdesc="Vimb is a WebKit-based web browser"
arch=('i686' 'x86_64')
url="http://fanglingsu.github.io/vimb"
license=('GPL3')
provides=('vimb')
depends=('webkit2gtk')
source=('git+https://github.com/fanglingsu/vimb.git')
md5sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed -e 's/-alpha/.0/' -e 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    if [ -e $SRCDEST/config.h ]; then
        msg "using custom config.h";
        cp $SRCDEST/config.h "$_pkgname/src"
    fi
}

build() {
    cd "$_pkgname"
    make V=1 DESTDIR="$pkgdir" PREFIX="/usr"
}

package() {
    cd "$_pkgname"
    make V=1 DESTDIR="$pkgdir" PREFIX="/usr" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

