# Maintainer: Daniel Carl
pkgname=dwm-git
_pkgname=dwm
pkgver=6.1.46.gd0e7e74
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
epoch=1
source=("$_pkgname::git://github.com/fanglingsu/dwm.git#branch=my")
sha1sums=('SKIP')

pkgver(){
  cd $_pkgname
  git describe --tags |sed 's/-/./g'
}

prepare() {
  cd "$_pkgname"

  if [ -e $startdir/config.h ]; then
    msg2 "Use custom config.h file"
    cp "$startdir/config.h" config.h
  fi

  sed -i config.mk \
    -e 's/^CPPFLAGS\s\+=/CPPFLAGS +=/' \
    -e 's/^CFLAGS\s\+=/CFLAGS +=/g' \
    -e 's/^LDFLAGS\s\+=/LDFLAGS +=/g'
}

build() {
  cd $_pkgname

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  make -C $_pkgname PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D $_pkgname/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=4 sts=2 sw=2 et:
