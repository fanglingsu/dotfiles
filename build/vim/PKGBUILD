# Maintainer: Daniel Carl
pkgbase=vim
pkgname=('vim' 'vim-runtime')
pkgver=8.2.0148
_versiondir=82
pkgrel=1
arch=(x86_64)
license=('custom:vim')
groups=('modified')
url="http://www.vim.org"
makedepends=(glibc libxt git)
source=(https://github.com/vim/vim/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz
        'vimrc'
        'archlinux.vim'
        'vimdoc.hook')
sha256sums=('235375de23ca95576f15f1d7178c0ef96ebae3a3de07d83a06c623aec9325ccd'
            '3e25423b126aa0ddd16359b265e6e2db5c4e93d9756d3d1fc465b691a510dea2'
            '0cf8b42732111d0c66c3908a76d832736e8f8dc3abef81cb092ddf84cb862ea2'
            '7095cafac21df7aa42749d6864d1c0549fe65771d8edda3102c931c60782b6b9')

prepare() {
  cd "$pkgbase-$pkgver/src"

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -E 's|^.*(#define SYS_.*VIMRC_FILE.*").*$|\1|g' -i feature.h
  sed -E 's|^.*(#define VIMRC_FILE.*").*$|\1|g' -i feature.h
  # enable conceal feature
  echo -e '#ifndef FEAT_CONCEAL\n#define FEAT_CONCEAL\n#endif\n' >> feature.h

  autoconf
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure \
    --prefix=/usr \
    --localstatedir=/var/lib/vim \
    --with-features=big \
    --with-compiledby='Daniel Carl' \
    --with-x=yes \
    --enable-acl \
    --enable-terminal \
    --disable-gui \
    --disable-netbeans \
    --enable-multibyte \
    --disable-gpm \
    --enable-pythoninterp

  make
}

check() {
  cd "$pkgbase-$pkgver"
  make -j1 test
}

package_vim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
  depends=("vim-runtime=$pkgver-$pkgrel" 'libxt' 'python2')
  conflicts=('gvim')

  cd "$pkgbase-$pkgver"
  make -j1 VIMRCLOC=/etc DESTDIR="$pkgdir" install

  # provided by (n)vi in core
  rm "$pkgdir"/usr/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r "$pkgdir"/usr/share/vim

  # remove gvim.desktop as not included
  rm "$pkgdir"/usr/share/applications/gvim.desktop

  # license
  install -Dm644 runtime/doc/uganda.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_vim-runtime() {
  pkgdesc='Runtime for vim and gvim'
  depends=('perl' 'gawk')
  backup=('etc/vimrc')

  cd "$pkgbase-$pkgver"

  make -j1 VIMRCLOC=/etc DESTDIR="$pkgdir" install
  # man and bin files belong to 'vim'
  rm -r "$pkgdir"/usr/share/man/ "$pkgdir"/usr/bin/

  # Don't forget logtalk.dict
  install -Dm644 runtime/ftplugin/logtalk.dict \
    "$pkgdir"/usr/share/vim/vim${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "$pkgdir"/usr/share/vim/vim${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    "$pkgdir"/usr/share/vim/vim${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    "$pkgdir"/usr/share/vim/vim${_versiondir}/ftplugin/changelog.vim

  # rc files
  install -Dm644 "$srcdir"/vimrc "$pkgdir"/etc/vimrc
  install -Dm644 "$srcdir"/archlinux.vim \
    "$pkgdir"/usr/share/vim/vimfiles/archlinux.vim

  # rgb.txt file
  install -Dm644 runtime/rgb.txt \
    "$pkgdir"/usr/share/vim/vim${_versiondir}/rgb.txt

  # no desktop files and icons
  rm -r "$pkgdir"/usr/share/{applications,icons}

  # license
  install -dm755 "$pkgdir"/usr/share/licenses/vim-runtime
  ln -s /usr/share/vim${_versiondir}/doc/uganda.txt \
    "$pkgdir"/usr/share/licenses/vim-runtime/license.txt

  # pacman hook for documentation helptags
  install -Dm 644 "$srcdir"/vimdoc.hook "$pkgdir"/usr/share/libalpm/hooks/vimdoc.hook
}

# vim:set sw=2 sts=2 et:
